( FIG-FORTH SPELL - decompiles existing words )
FORTH DEFINITIONS HEX

: 0. DUP 10 < IF 30 EMIT THEN . ;
: .CFA SPACE DUP C@ 0. 1+ DUP C@ 0. ;
: PRTVAL CR DROP 2+ ." PFA " DUP DUP . .CFA DROP ;
: STOP 2 SPACES @ 24 EMIT . DECIMAL CR ." Ok" QUIT ;
: COL? ' : 12 + = IF ."   DOCOLON" THEN ;
: CON? ' CONSTANT 8 + = IF ."   DOCONSTANT " PRTVAL STOP THEN ;
: VAR? ' VARIABLE 4 + = IF ."   DOVARIABLE " PRTVAL STOP THEN ;
: USE? ' USER 4 + = IF ."   DOUSER " PRTVAL STOP THEN ;
: .A 4 SPACES . ;
: DOES? ' DOES> 0A + = IF ."   DODOES"
    PRTVAL ."    DOES> ADDRESS--" 24 EMIT DUP @ . CR 2+
    CR QUIT THEN ;
: CODE? OVER 2+ = IF ."   MACHINE CODE FOLLOWS" DUP
    PRTVAL CR DECIMAL ."     ETC." CR CR ." Ok" DROP
    QUIT THEN ;
: .BRAN DUP .CFA 3 SPACES 24 EMIT SWAP @ OVER + 1 -
    . 1+ CR ;
: .VAL DUP .CFA 2 SPACES 24 EMIT SWAP @ . 1+ CR ;
: .STR 0 DO 1+ DUP .A SPACE DUP C@ DUP . 6 SPACES 7F AND
    EMIT CR LOOP ;
: MISC?
    DUP ' LIT = IF DROP DUP .A .VAL
    ELSE DUP ' BRANCH = IF DROP DUP .A .BRAN
    ELSE DUP ' 0BRANCH = IF DROP DUP .A .BRAN
    ELSE DUP ' (LOOP) = IF DROP DUP .A .BRAN
    ELSE DUP ' (+LOOP) = IF DROP DUP .A .BRAN
    ELSE ' (.") = IF DUP 4 - @ ' COMPILE 2 - = 0=
    IF DUP .A SPACE DUP C@ DUP 0. 5 SPACES
    ." LENGTH-BYTE OF STRING" CR .STR 1+ THEN
    THEN THEN THEN THEN THEN THEN ;
: ;CHECK DUP ' ;S = IF DROP 1 ELSE ' (;CODE) = DUP IF DROP
    OVER DUP .A .CFA CR ."     ETC." CR THEN THEN ;
: SPELL [COMPILE] ' HEX CR CR ." NFA " NFA DUP . SPACE DUP
    C@ DUP . 5 SPACES ." LENGTH-BYTE" CR 1F AND .STR 1+
    DUP .A .CFA 2 SPACES ." LINK TO" SPACE DUP 1 - @ ID.
    CR 1+ ." CFA" SPACE DUP . DUP DUP @ SWAP .CFA DROP
    DUP COL? DUP CON? DUP VAR? DUP USE? DUP DOES? CODE?
    CR 2+ ." PFA " BEGIN DUP DUP . .CFA 1+ SWAP @ 2+ DUP
    NFA 2 SPACES ID. CR PAUSE IF DROP DROP CR QUIT THEN
    DUP ;CHECK 0= IF MISC? 0 ELSE DROP 1 THEN
    4 SPACES PAUSE OR UNTIL DROP CR ;

( Usage: SPELL <word from dictionary> <enter> )
